#!perl

use 5.010001;
use strict;
use warnings;
use Log::ger;
#use Log::ger::Screen;

use CLI::MetaUtil::Getopt::Long qw(GetOptionsCLIWrapper);
use File::Temp qw(tempdir);
use IPC::System::Options 'system', -log=>1, -die=>1;

# AUTHORITY
# DATE
# DIST
# VERSION

# XXX duplication with diff-pdf-text
my $opt_transpose_csv;

my $tempdir;
my $filename;

my $prog = "diff-pdf-text-between-conflicted-git-versions";
my ($path_ours, $path_theirs);
GetOptionsCLIWrapper(
    cli => 'diff',
    add_opts => {
        '<>' => sub {
            die "Please specify only one filename" if defined $filename;
            $filename = $_[0];
            (my $basename = $filename) =~ s!.+/!!;
            my ($content_ours, $content_theirs);
            system(
                {capture_stdout => \$content_ours},
                "git", "show", ":2:$filename",
            );
            system(
                {capture_stdout => \$content_theirs},
                "git", "show", ":3:$filename",
            );

            $tempdir = File::Temp::tempdir(CLEANUP => !$ENV{DEBUG});
            log_trace "Created temporary directory %s", $tempdir;

            $path_ours = "$tempdir/ours.$basename";
            open my $fh_ours  , ">", $path_ours
                or die "$prog: Can't write ours: $!";
            log_trace "Created temporary file for 'ours' PDF %s", $path_ours;
            print $fh_ours $content_ours;
            close $fh_ours
                or die "$prog: Can't write ours: $!";

            $path_theirs = "$tempdir/theirs.$basename";
            open my $fh_theirs, ">", $path_theirs
                or die "$prog: Can't write theirs: $!";
            log_trace "Created temporary file for 'theirs' PDF %s", $path_theirs;
            print $fh_theirs $content_theirs;
            close $fh_theirs
                or die "$prog: Can't write theirs: $!";
        },
    },
);

die "Please specify file" unless defined $path_ours;
system(
    "diff-pdf-text", @ARGV,
    $path_ours, $path_theirs,
);

# ABSTRACT: Diff the text of PDF file between conflicted git versions
# PODNAME:

=head1 SYNOPSIS

 % diff-pdf-text-between-conflicted-git-versions [options] <FILE>


=head1 DESCRIPTION

This is a wrapper for L<diff-pdf-text> to compare two versions ("ours" and
"theirs") when there is a git merge conflict.


=head1 OPTIONS

See L<diff-pdf-text>.


=head1 ENVIRONMENT

=head2 DEBUG

If set to true, do not cleanup temporary files.

To see which temporary files are created, use:

 % PERL5OPT=-MLog::ger::Screen TRACE=1 diff-pdf-text-between-conflicted-git-versions ...


=head1 SEE ALSO

L<diff-pdf-text>

Unix command L<diff>.
